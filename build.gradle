apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
	compile 'org.apache.logging.log4j:log4j-api:2.3'
	compile 'log4j:log4j:1.2.17'
	compile 'org.slf4j:slf4j-api:1.7.12'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'
version             = '1.4.0'
project.archivesBaseName = 'debugtrace'

compileJava {
	options.encoding     = 'UTF-8'
	options.deprecation  = true
	options.debug        = true
	options.compilerArgs = ['-Xlint']
}

jar {
	manifest {
		attributes	'Implementation-Title'    : project.name,
					'Implementation-Version'  : version,
					'Implementation-Vendor'   : 'Masato Kokubo',
					'Implementation-Vendor-Id': 'org.mkokubo',
					'Main-Class'              : 'org.mkokubo.debugtrace.DebugTrace'

	}
}

javadoc {
	title               = "$project.name $version API Specification"
	options.locale      = 'en'
	options.windowTitle = "$project.name"
	options.overview    = 'src/main/java/overview.html'

	options.addBooleanOption('author', true)
	options.addStringOption ('bottom', '<div class="copyright">&copy;2015 Masato Kokubo</div>')
}

javadoc.doLast {
	new File("src/main/java/addtional.css").eachLine {
		new File("build/docs/javadoc/stylesheet.css").append("$it\n")
	}
}

task zip(type: Zip) {
	classifier = 'bin'

	into('/'  , {from 'build/libs'})
	into('/'  , {from 'LICENSE.txt', 'README.txt'})
	into('doc', {from 'build/docs/javadoc'})
	into('resources-sample', {from 'src/test/resources'})
}
