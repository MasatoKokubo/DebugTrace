// (C) 2015 Masato Kokubo
plugins {
    id 'com.jfrog.bintray' version '1.8.5'
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java-library'
apply plugin: 'groovy'

repositories {
    jcenter()
}

dependencies {
    compileOnly        'log4j:log4j:1.+'
    testRuntimeOnly    'log4j:log4j:1.+'

    compileOnly        'org.apache.logging.log4j:log4j-api:2.+'
    testRuntimeOnly    'org.apache.logging.log4j:log4j-core:2.+'

    compileOnly        'org.slf4j:slf4j-api:1.7.+'
    testRuntimeOnly    'ch.qos.logback:logback-classic:1.+'

    compileOnly        'org.apache.commons:commons-dbcp2:2.+'
    testRuntimeOnly    'org.apache.commons:commons-dbcp2:2.+'

    testCompileOnly    'org.junit.jupiter:junit-jupiter-api:5.6.+'
    testRuntimeOnly    'org.junit.vintage:junit-vintage-engine:5.6.+'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.+'
    testCompileOnly    'org.spockframework:spock-core:1.3-groovy-2.5'
}

sourceCompatibility = 8
targetCompatibility = 8
group               = 'org.debugtrace'
archivesBaseName    = 'debugtrace'
version             = '3.0.2'

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
        groovy {
            srcDir 'src/test/groovy'
        }
        resources {
            srcDir 'src/test/resources'
        }
    }
    example {
        java {
            srcDir 'src/example/java'
        }
        compileClasspath += main.output
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += main.output
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

wrapper {
    gradleVersion = '6.5.1'
}

compileJava {
    options.encoding     = 'UTF-8'
    options.deprecation  = true
    options.debug        = true
    options.compilerArgs = ['-Xlint']
}

compileTestJava {
    options.encoding     = compileJava.options.encoding
    options.deprecation  = compileJava.options.deprecation
    options.debug        = compileJava.options.debug
    options.compilerArgs = compileJava.options.compilerArgs
}

task compileJava_ja(type:JavaCompile) {
    options.encoding     = compileJava.options.encoding
    options.deprecation  = compileJava.options.deprecation
    options.debug        = compileJava.options.debug
    options.compilerArgs = compileJava.options.compilerArgs

    classpath      = compileJava.classpath
    source         = files('src/main_ja/java')
    destinationDir = file('build/classes/main_ja')
}

jar {
    manifest {
        attributes  'Implementation-Title'    : project.name,
                    'Implementation-Version'  : version,
                    'Implementation-Vendor'   : 'Masato Kokubo',
                    'Implementation-Vendor-Id': 'jp.masatokokubo'
    }
}

javadoc {
    title               = "$project.name $version API Specification"
    options.charSet     = 'UTF-8'
    options.encoding    = 'UTF-8'
    options.locale      = 'en'
    options.windowTitle = "$project.name"
    options.addBooleanOption('author', true)
    options.addStringOption ('bottom', '<div class="copyright">&copy;2015 Masato Kokubo</div>')
}

javadoc.doFirst {
    def cssFile = file('build/docs/javadoc/stylesheet.css')
    if (cssFile.exists())
        cssFile.delete()
}

javadoc.doLast {
    def cssFile = file('build/docs/javadoc/stylesheet.css')
    file('src/main/java/addtional.css').eachLine {
        cssFile.append("$it\n")
    }
}

task zip(type: Zip) {
    classifier = 'bin'

    into('/'      , {from "build/libs/${archivesBaseName}-${version}.jar"})
    into('/'      , {from 'LICENSE.txt', 'README.asciidoc', 'README_ja.asciidoc'})
    into('javadoc', {from 'build/docs/javadoc'})
    into('resources-example', {from 'src/test/resources'})
}

task sourcesJar(type: Jar) {
    classifier = 'sources'

    manifest {
        attributes 'Implementation-Title'    : project.name,
                   'Implementation-Version'  : version,
                   'Implementation-Vendor'   : 'Masato Kokubo',
                   'Implementation-Vendor-Id': 'jp.masatokokubo'
    }

    into('/org', {from 'src/main/java/org'})
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
//            from components.java
            groupId    group
            artifactId archivesBaseName
            version    version
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key  = project.hasProperty('bintray_api_key') ? bintray_api_key : ''
    configurations = ['archives']
    publications   = ['mavenPublication']

//    dryRun = true

    pkg {
        repo            = 'maven'
        name            = 'DebugTrace-java'
        websiteUrl      = 'https://github.com/MasatoKokubo/DebugTrace-java'
        issueTrackerUrl = 'https://github.com/MasatoKokubo/DebugTrace-java/issues'
        vcsUrl          = 'https://github.com/MasatoKokubo/DebugTrace-java.git'
        licenses        = ['MIT']
        version {
            name           = project.version
            released       = new Date()
            vcsTag         = project.version
            gpg {
                sign       = true
                passphrase = project.hasProperty('bintray_gpg_password') ? bintray_gpg_password : ''
            }
        }
    }
}
